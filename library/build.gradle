apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

repositories {
  jcenter()
}

android {
  compileSdkVersion 21
  buildToolsVersion "21.1.2"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 21
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }

  packagingOptions {
    exclude 'LICENSE.txt'
  }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile 'com.android.support:support-v13:21.0.3'

  androidTestCompile "com.android.support.test:testing-support-lib:0.1"
  androidTestCompile "com.squareup.assertj:assertj-android:1.0.0"
}

group = project.GROUP
version = project.VERSION

install {
  repositories.mavenInstaller {
    pom {
      project {
        packaging 'aar'
        name project.POM_NAME
        url project.POM_URL
        licenses {
          license {
            name project.POM_LICENSE_NAME
            url project.POM_LICENSE_URL
          }
        }
        scm {
          connection project.POM_SCM_CONNECTION
          developerConnection project.POM_SCM_DEV_CONNECTION
          url project.POM_SCM_URL
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}
